# name=cilvaa-0cfa-dlog

.include "LV.dom"
.include "V.dom"
.include "H.dom"
.include "F.dom"
.include "P.dom"

.bddvarorder P0_V0xV1_LV0xLV1_H0_F0xF1

###
# Relations
###

StatLV(lv:LV) input

VH(v:V,h:H) input
FH(f:F,h:H) input
LVH(lv:LV,h:H) input

VFLV(v:V,f:F,lv:LV) input
SFLV(s:F0,f:F1,lv:LV) input
LVFLV(lv0:LV0,f:F,lv1:LV1) input

#MV(m:M,v:V) input
#MLV(m:M,lv:LV) input

PliveV(p:P,v:V) input
PliveLV(p:P,lv:LV) input

sameScopeLVLV(lv0:LV0,lv1:LV1) 
sameScopeVV(v0:V0,v1:V1) 
sameScopeVLV(v:V,lv:LV)

samePtsVV(v0:V0,v1:V1) 
samePtsVS(v:V,s:F) 
#samePtsSS(s0:F0,s1:F1) 
samePtsSLV(s:F,lv:LV) 
samePtsVLV(v:V,lv:LV) 
samePtsLVLV(lv0:LV0,lv1:LV1) 

aliasLVLV(lv0:LV0,lv1:LV1) output

###
# Constraints
###

sameScopeVV(v0,v1) :- v0 != v1, PliveV(p,v0), PliveV(p,v1).
sameScopeVLV(v,lv) :- PliveV(p,v), PliveLV(p,lv).
sameScopeLVLV(lv0,lv1) :- lv0 != lv1, PliveLV(p,lv0), PliveLV(p,lv1).

samePtsVV(v0,v1) :- sameScopeVV(v0,v1), VH(v0,h), VH(v1,h). # commutative
#samePtsSS(s0,s1) :- s0 != s1, FH(s0,h), FH(s1,h). # commutative
samePtsSLV(s,lv) :- FH(s,h), LVH(lv,h).
samePtsVS(v,s) :- VH(v,h), FH(s,h).
samePtsVLV(v,lv) :- sameScopeVLV(v,lv), VH(v,h), LVH(lv,h). 
samePtsVLV(v,lv) :- VH(v,h), StatLV(lv), LVH(lv,h). 
samePtsLVLV(lv0,lv1) :- sameScopeLVLV(lv0,lv1), LVH(lv0,h), LVH(lv1,h). # commutative
samePtsLVLV(lv0,lv1) :- StatLV(lv0), LVH(lv0,h), LVH(lv1,h).
#samePtsLVLV(lv0,lv1) :- StatLV(lv1), LVH(lv0,h), LVH(lv1,h).

aliasLVLV(lv0,lv1) :- samePtsVV(v0,v1), VFLV(v0,f,lv0), VFLV(v1,f,lv1). 
#aliasLVLV(lv0,lv1) :- samePtsSS(s0,s1), SFLV(s0,f,lv0), SFLV(s1,f,lv1). 
aliasLVLV(lv0,lv1) :- samePtsVS(v,s), VFLV(v,f,lv0), SFLV(s,f,lv1). 
aliasLVLV(lv0,lv1) :- samePtsVS(v,s), VFLV(v,f,lv1), SFLV(s,f,lv0).
aliasLVLV(lv0,lv1) :- samePtsVLV(v,lv), VFLV(v,f,lv0), LVFLV(lv,f,lv1). 
aliasLVLV(lv0,lv1) :- samePtsVLV(v,lv), VFLV(v,f,lv1), LVFLV(lv,f,lv0). 
aliasLVLV(lv0,lv1) :- samePtsSLV(s,lv), SFLV(s,f,lv0), LVFLV(lv,f,lv1). 
aliasLVLV(lv0,lv1) :- samePtsSLV(s,lv), SFLV(s,f,lv1), LVFLV(lv,f,lv0). 
aliasLVLV(lv0,lv1) :- samePtsLVLV(lv2,lv3), LVFLV(lv2,f,lv0), LVFLV(lv3,f,lv1). 

